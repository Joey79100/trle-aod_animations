// My guess at the TRLE *.SFX file format.
// Note this is only for illustration.
// Code was not compiled so may be errors!
//                       ~ sapper Feb 2014

// FreePascal code

type TSoundDetail = packed record       // 8 bytes
                  sampleIndex           : word; // index into samples array
                  volume, range, chance : byte;
                  pitch                 : shortint;
                  SamplesAndFlags       : word;
                  end;


function GetNumSamples(sd : TSoundDetail): integer;
var num : word;
begin
  Result := 0;
  num := (sd.SamplesAndFlags and $003C) >> 2;
  Result := integer(num);
end;


function GetFlags(sd : TSoundDetail): string;
var P,V,N,L,R,W : string;
begin
  //16 bit bit field
  //msb ........................................ lsb
  // 0 v p n   s9 s8 s7 s6   s5 s4 s3 s2   s1 s0 r w
  // L sets r and w
  // s3 to s0 is Num Samples (s9 to s4 not used)
  // So maximum number of samples is 15 per sound.

  Result := '';
  P := ''; V := '' ; N := ''; L := ''; R := '';

  if (sd.SamplesAndFlags and $C000) = $4000 then V := 'V';

  if (sd.SamplesAndFlags and $3000) = $3000 then begin P := 'P'; N := 'N'; end
  else if (sd.SamplesAndFlags and $3000) = $2000 then P := 'P'
  else if (sd.SamplesAndFlags and $3000) = $1000 then N := 'N';

  if (sd.SamplesAndFlags and $0003) = $0003 then L := 'L'
  else if (sd.SamplesAndFlags and $0003) = $0002 then R := 'R';

  Result := Format('%-2s%-2s%-2s%-2s%-2s',[P,V,R,N,L]);
end;


function ReadSFX(var wad :TWad; const filename : string) : Boolean;
var
  f            : file;
  numSounds,
  numSamples   : integer;
  sfx          : array of smallint;
  samples      : array of integer;
  soundDetails : array of TSoundDetail;
begin
  Result := False;

  assignfile(f, filename);
  {$I-} reset(f, 1); {$I+}
  if IOResult <> 0 then Exit;

  SetLength(sfx, 370);
  if wad.Version = 130 then
  begin
     SetLength(sfx, 2048);
  end;
  numSounds := 0;
  numSamples := 0;
  try
     blockread(f, sfx[0], Length(sfx)* SizeOf(smallint));
     // sfx[j] = -1 if sound ID j is not used
     // otherwise sfx[j] is n-th sound used
     // i.e. first sound ID used has sfx[j] = 0
     //     second sound ID used has sfx[j] = 1, etc
     blockread(f, numSounds, SizeOf(numSounds));
     if numSounds > 0 then
     begin
       SetLength(soundDetails,numSounds);
       blockread(f, soundDetails[0], numSounds * SizeOf(TSoundDetail));
     end;
     blockread(f, numSamples, SizeOf(numSamples));
     if numSamples > 0 then
     begin
       SetLength(samples, numSamples);
       blockread(f, samples[0], numSamples * SizeOf(integer));
       // samples[j] is index of sample name in the list of all sample names
       // contained in sounds.txt.
       // The first sample is number 0 and if a sound is not associated with any wad
       // those samples are not included in the list of samples.
       // I think this array is not used by the engine, the sampleIndex is used
     end;
  finally
     CloseFile(f);
  end;
  Result := True;
end;
